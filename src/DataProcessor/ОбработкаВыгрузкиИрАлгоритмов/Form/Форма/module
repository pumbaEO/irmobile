
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	ОчиститьСообщения(); 
	
	Если Прав(КаталогВыгрузки, 1) <> "\" Тогда
		
		КаталогВыгрузки = КаталогВыгрузки + "\";
		
	КонецЕсли; 
	
	Попытка
		КаталогВыгрузкиФайл = Новый Файл(КаталогВыгрузки);
		Если НЕ КаталогВыгрузкиФайл.Существует() ИЛИ НЕ КаталогВыгрузкиФайл.ЭтоКаталог() Тогда			
			Ответ = Вопрос(НСтр("ru = 'Каталог не существует. Создать?'"), РежимДиалогаВопрос.ОКОтмена, 60);
			Если Ответ = КодВозвратаДиалога.ОК Тогда
				СоздатьКаталог(КаталогВыгрузкиФайл.ПолноеИмя);
			Иначе
				Возврат;
			КонецЕсли; 				
		КонецЕсли; 
	Исключение
		Предупреждение("Не удалось создать каталог!");
		Возврат;
	КонецПопытки;	
	
	ЗамещатьФайлы = Истина;
	Если НайтиФайлы(КаталогВыгрузки, "*.alg", Ложь).Количество() > 0 Тогда
		
		Ответ = Вопрос(НСтр("ru = 'В указанном каталоге обнаружены файлы алгоритмов. Замещать данные файлы при совпадении имен?'"), РежимДиалогаВопрос.ДаНетОтмена, 60);
		Если Ответ = КодВозвратаДиалога.Отмена Тогда			
			Возврат;                                			
		Иначе 
			ЗамещатьФайлы = (Ответ = КодВозвратаДиалога.Да);
		КонецЕсли; 
		
	КонецЕсли; 
	
	ВыборкаАлгоритмов = Справочники.ирАлгоритмы.Выбрать();
	Пока ВыборкаАлгоритмов.Следующий() Цикл
		
		ФайлАлгоритма = Новый Файл(КаталогВыгрузки + ВыборкаАлгоритмов.Наименование + ".alg");
		Если ФайлАлгоритма.Существует() И НЕ ЗамещатьФайлы Тогда
			
			Сообщить("Не выгружен алгоритм " + ВыборкаАлгоритмов.Наименование + " из-за совпадения имен", СтатусСообщения.Информация);
			Продолжить;
			
		КонецЕсли; 
		
		Алгоритм = ПолучитьВыгружаемыйАлгоритм(ВыборкаАлгоритмов);
		Попытка
			ЗначениеВФайл(ФайлАлгоритма.ПолноеИмя, Алгоритм);
			Сообщить("Выгружен алгоритм " + ВыборкаАлгоритмов.Наименование, СтатусСообщения.Информация);
		Исключение
			Сообщить("Не выгружен алгоритм " + ВыборкаАлгоритмов.Наименование + " из-за неизвестной ошибки", СтатусСообщения.Информация);
		КонецПопытки;
			
	КонецЦикла; 
	
КонецПроцедуры

Процедура КаталогВыгрузкиНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.ПолноеИмяФайла = "";
	Если Диалог.Выбрать() Тогда
		КаталогВыгрузки = Диалог.Каталог;		
	КонецЕсли;
	
КонецПроцедуры   

Функция ПолучитьВыгружаемыйАлгоритм(ДанныеАлгоритма)

	Результат = Новый Структура("ТекстАлгоритма, ДатаИзменения, Комментарий, Параметры");
	Результат.ТекстАлгоритма = ДанныеАлгоритма.ТекстАлгоритма;
	Результат.ДатаИзменения = ДанныеАлгоритма.ДатаИзменения;
	Результат.Комментарий = ДанныеАлгоритма.Комментарий;
	Результат.Параметры = ДанныеАлгоритма.Параметры.Выгрузить();
	Возврат Результат;
	
КонецФункции // ПолучитьВыгружаемыйАлгоритм()
 


