Перем мСловарьПланаЗапроса;

Процедура ПриОткрытии()
	
	мСловарьПланаЗапроса = ирНеглобальный.ПолучитьТаблицуИзТабличногоДокументаЛкс(ОбработкаОбъект.ПолучитьМакет("ОперацииПланаЗапросаMSSQL"));
	#Если _ Тогда
	    мСловарьПланаЗапроса = Новый ТаблицаЗначений;
	#КонецЕсли
	мСловарьПланаЗапроса.Индексы.Добавить("Ключ");
	КолонкиПланаЗапроса = ирНеглобальный.ПолучитьТаблицуИзТабличногоДокументаЛкс(ОбработкаОбъект.ПолучитьМакет("КолонкиПланаЗапросаMSSQL"));
	#Если _ Тогда
	    КолонкиПланаЗапроса = Новый ТаблицаЗначений;
	#КонецЕсли
	Для Каждого СтрокаКолонки Из КолонкиПланаЗапроса Цикл
		ЭлементыФормы.ДеревоПлана.Колонки[СтрокаКолонки.Имя].ПодсказкаВШапке = СтрокаКолонки.Описание;
	КонецЦикла;
	RegExp = ирКэш.Получить().RegExp;
	ШаблонЧисла = "([^,_\n]*),";
	ШаблонНепечатного = "\s*";
	RegExp.Pattern = ""
		+ ШаблонЧисла + ШаблонНепечатного //1
		+ ШаблонЧисла + ШаблонНепечатного //2
		+ ШаблонЧисла + ШаблонНепечатного //3
		+ ШаблонЧисла + ШаблонНепечатного //4
		+ ШаблонЧисла + ШаблонНепечатного //5
		+ ШаблонЧисла + ШаблонНепечатного //6
		+ ШаблонЧисла + ШаблонНепечатного //7
		+ ШаблонЧисла                     //8
		+ "([^\n]*)\n";                   //9
	РезультатПоиска = RegExp.Execute(Текст);
	МаркерИнструкции = "|--";
	НомерОперации = 1;
	ИндексПервойКолонкиПланаЗапрос = 1;
	Для Каждого Вхождение Из РезультатПоиска Цикл
		ТекстИнструкции = Вхождение.SubMatches(8);
		ПозицияПалки = Найти(ТекстИнструкции, МаркерИнструкции);
		Уровень = (ПозицияПалки - 4) / 5 + 1;
		СтрокаДерева = ДобавитьСтрокуДерева(Уровень);
		СтрокаДерева.StmtText = Сред(ТекстИнструкции, ПозицияПалки + СтрДлина(МаркерИнструкции));
		СтрокаДерева.Operator = ЛксПолучитьПервыйФрагмент(СтрокаДерева.StmtText, "(");
		СтрокаСловаря = мСловарьПланаЗапроса.Найти(СтрокаДерева.Operator, "Ключ");
		СтрокаДерева.Оператор = СтрокаСловаря.Название;
		СтрокаДерева.НомерОперации = НомерОперации;
		Для Индекс = 0 По 7 Цикл
			СтрокаДерева[Индекс + ИндексПервойКолонкиПланаЗапрос] = Вхождение.SubMatches(Индекс);
		КонецЦикла;
		НомерОперации = НомерОперации + 1;
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьСтрокуДерева(Уровень)
	
	СтрокиУровня = ДеревоПлана.Строки;
	Для Счетчик = 2 По Уровень Цикл
		СтрокиУровня = СтрокиУровня[СтрокиУровня.Количество() - 1].Строки;
	КонецЦикла;
	Результат = СтрокиУровня.Добавить();
	
	Возврат Результат;
	
КонецФункции

Процедура ДеревоПланаПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	СтрокаСловаря = мСловарьПланаЗапроса.Найти(ДанныеСтроки.Operator, "Ключ");
	Если ТипЗнч(СтрокаСловаря.Картинка) = Тип("Картинка") Тогда
		ОформлениеСтроки.Ячейки.StmtText.УстановитьКартинку(СтрокаСловаря.Картинка);
	КонецЕсли; 
	//ирНеглобальный.ОформитьФонТекущейСтрокиЛкс(Элемент, ОформлениеСтроки, ДанныеСтроки);
	
КонецПроцедуры

Процедура ДеревоПланаПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущаяСтрока = Неопределено Тогда
		ЭтаФорма.ОписаниеОперации = "";
		ЭтаФорма.Инструкция = "";
		Возврат;
	КонецЕсли; 
	СтрокаСловаря = мСловарьПланаЗапроса.Найти(Элемент.ТекущаяСтрока.Operator, "Ключ");
	ЭтаФорма.ОписаниеОперации = СтрокаСловаря.Описание;
	ЭтаФорма.Инструкция = Элемент.ТекущаяСтрока.StmtText;
	
КонецПроцедуры
