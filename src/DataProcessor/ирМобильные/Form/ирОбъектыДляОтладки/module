
Процедура КнопкаВыполнитьНажатие(Кнопка)
	// Вставить содержимое обработчика.
КонецПроцедуры

Процедура КоманднаяПанель2Удалить(Кнопка)
	
	ТекущиеДанные = ЭлементыФормы.ОбъектыДляОтладки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ХранилищеОбщихНастроек.Удалить(ОбработкаОбъект.мКлючОбъектаДляСохраненияОбъектовОтладки, ТекущиеДанные.Код, ИмяПользователя());
	ТекущаяСтрока = ЭлементыФормы.ОбъектыДляОтладки.ТекущаяСтрока;
	ОбъектыДляОтладки.Удалить(ТекущаяСтрока);
	ЭлементыФормы.ПолеТекстовогоДокумента.УстановитьТекст("");
	
КонецПроцедуры

Процедура КоманднаяПанель2Отладить(Кнопка)
	
	ТекущиеДанные = ЭлементыФормы.ОбъектыДляОтладки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 

	ирНеглобальный.ОтладитьОбъектДляОтладки(ТекущиеДанные.XML);
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	 	
	ОбновитьСписокОбъектовДляОтладки();
	
КонецПроцедуры

Процедура ОбъектыДляОтладкиНаименованиеПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.ОбъектыДляОтладки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	СохранитьНастройки(ТекущиеДанные);
	
КонецПроцедуры

Процедура СохранитьНастройки(ДанныеНастроек)

	Настройки = Новый Структура;
	Настройки.Вставить("Код", ДанныеНастроек.Код);
	Настройки.Вставить("Наименование", ДанныеНастроек.Наименование);
	Настройки.Вставить("XML", ДанныеНастроек.XML);	
	Настройки.Вставить("ДатаИзменения", ТекущаяДата());
	Настройки.Вставить("ЧислоСимволов", СтрДлина(ДанныеНастроек.XML));
	ХранилищеОбщихНастроек.Сохранить(мКлючОбъектаДляСохраненияОбъектовОтладки, ДанныеНастроек.Код, Настройки, , ИмяПользователя());
	ДанныеНастроек.ДатаИзменения = Настройки.ДатаИзменения;
	ДанныеНастроек.ЧислоСимволов = Настройки.ЧислоСимволов;
		
КонецПроцедуры // СохранитьНастройки()

Процедура ОбъектыДляОтладкиПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = ЭлементыФормы.ОбъектыДляОтладки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ЭлементыФормы.ПолеТекстовогоДокумента.УстановитьТекст(ТекущиеДанные.XML);
	
КонецПроцедуры   

Процедура КоманднаяПанель2Записать(Кнопка)
	
	ТекущиеДанные = ЭлементыФормы.ОбъектыДляОтладки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ТекущиеДанные.XML = ЭлементыФормы.ПолеТекстовогоДокумента.ПолучитьТекст();
	
	СохранитьНастройки(ТекущиеДанные);

КонецПроцедуры

Процедура ОбновитьСписокОбъектовДляОтладки()
	
	Если ОбъектыДляОтладки.Колонки.Найти("XML") = Неопределено Тогда
		
		ОбъектыДляОтладки.Колонки.Добавить("XML");
		
	КонецЕсли; 
	
	ОбъектыДляОтладки.Очистить();
	
	Если ирПлатформа.ВерсияПлатформы >= 802015 Тогда
		
		Выборка = ХранилищеОбщихНастроек.Выбрать(Новый Структура("КлючОбъекта, Пользователь", мКлючОбъектаДляСохраненияОбъектовОтладки, ИмяПользователя()));
		Пока Выборка.Следующий() Цикл
		
			ЗаполнитьЗначенияСвойств(ОбъектыДляОтладки.Добавить(), Выборка.Настройки);
			
		КонецЦикла;
		
	Иначе
		
		СписокНастроек = ХранилищеОбщихНастроек.ПолучитьСписок(ОбработкаОбъект.мКлючОбъектаДляСохраненияОбъектовОтладки);
		Для Каждого КлючНастроек Из СписокНастроек Цикл
			
			Настройки = ХранилищеОбщихНастроек.Загрузить(ОбработкаОбъект.мКлючОбъектаДляСохраненияОбъектовОтладки, КлючНастроек.Значение);
			ЗаполнитьЗначенияСвойств(ОбъектыДляОтладки.Добавить(), Настройки);			
			
		КонецЦикла; 
		
	КонецЕсли; 	
		
КонецПроцедуры

Процедура КоманднаяПанель2Обновить(Кнопка)
	
	ОбновитьСписокОбъектовДляОтладки();
	
КонецПроцедуры


 
