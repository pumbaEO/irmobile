Перем мМакетныйОбъект;
Перем мТекущаяГруппаТипаМетаданных;
Перем мСоответствиеСтрокДереваИМетаданных;
Перем мВыборкаРезультата;
Перем мРезультатЗапроса;

Процедура ДобавитьМетаданныеДанныеВСтруктуруДерева(СтруктураДерева, Знач МетаданныеОбъекта, Знач ИмяОтображения, Знач ИмяДанных, 
	Знач НомерКартинкиМетаданных, Знач НомерКартинкиЭлементаМетаданных, Знач НомерКартинкиКонечногоЭлемента)
	
	СтруктураДанных = Новый Структура();
	СтруктураДанных.Вставить("Метаданные", МетаданныеОбъекта);
	СтруктураДанных.Вставить("ИмяОтображения", ИмяОтображения);
	СтруктураДанных.Вставить("ИмяДанных", ИмяДанных);
	СтруктураДанных.Вставить("НомерКартинкиМетаданных", НомерКартинкиМетаданных);
	СтруктураДанных.Вставить("НомерКартинкиЭлементаМетаданных", НомерКартинкиЭлементаМетаданных);
	СтруктураДанных.Вставить("НомерКартинкиКонечногоЭлемента", НомерКартинкиКонечногоЭлемента);
	
	СтрокаДерева = ДеревоОбмена.Строки.Добавить();
	СтрокаДерева.Имя = ИмяОтображения;
	СтрокаДерева.Представление = ИмяОтображения;
	СтрокаДерева.Данные = ИмяДанных;
	
	СтруктураДанных.Вставить("СтрокаДерева", СтрокаДерева);
	
	мСоответствиеСтрокДереваИМетаданных.Добавить(СтруктураДанных);
	
КонецПроцедуры

//Функция возвращает элемент соответсвия массива по метаданным
Функция ПолучитьЭлементСоответствияПоМетаданным(СоответствиеСтрокДереваИМетаданных, МетаданныеОбъекта)
	
	Для Каждого СтрокаСоответствия Из СоответствиеСтрокДереваИМетаданных Цикл
		
		Если СтрокаСоответствия.Метаданные.Индекс(МетаданныеОбъекта) > -1 Тогда
				
			Возврат СтрокаСоответствия;
						
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

//Функция получить элемент соответсвия по имени данных
Функция ПолучитьЭлементСоответствияПоИмениДанных(СоответствиеСтрокДереваИМетаданных, ИмяДанных)
	
	Для Каждого СтрокаСоответствия Из СоответствиеСтрокДереваИМетаданных Цикл
			
		Если СтрокаСоответствия.ИмяДанных = ИмяДанных Тогда
				
			Возврат СтрокаСоответствия;
						
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

//Функция возвращает номер картинки для элемента отображения данных
Функция ПолучитьНомерКартинкиПоДанным(УровеньДерева, ДанныеСтроки)
	
	Если УровеньДерева = 0 Тогда
		
		СтрокаСоответствия = ПолучитьЭлементСоответствияПоИмениДанных(мСоответствиеСтрокДереваИМетаданных, ДанныеСтроки.Данные);
		Возврат СтрокаСоответствия.НомерКартинкиМетаданных;
		
	ИначеЕсли УровеньДерева = 1 Тогда
		
		ИмяДанныхРодителя = ДанныеСтроки.Родитель.Данные;
		СтрокаСоответствия = ПолучитьЭлементСоответствияПоИмениДанных(мСоответствиеСтрокДереваИМетаданных, ИмяДанныхРодителя);
		Возврат СтрокаСоответствия.НомерКартинкиЭлементаМетаданных;
		
	ИначеЕсли УровеньДерева = 2 Тогда
		
		ИмяДанныхРодителя = ДанныеСтроки.Родитель.Родитель.Данные;
		
		Если ИмяДанныхРодителя = "Документы" Тогда
			
			Если ДанныеСтроки.Удален = Истина Тогда
				Возврат 13;
			ИначеЕсли ДанныеСтроки.Данные.Проведен Тогда
				Возврат 8;
			ИначеЕсли ДанныеСтроки.Данные.ПометкаУдаления Тогда
				Возврат 9;
			Иначе
				Возврат 10;
			КонецЕсли;
		
		ИначеЕсли (СформироватьПрефиксТипаСсылки(ИмяДанныхРодителя) <> "") Тогда
			
			Если ДанныеСтроки.Удален = Истина Тогда
				Возврат 13;	
			ИначеЕсли ДанныеСтроки.Данные.ПометкаУдаления Тогда
				Возврат 11;
			Иначе
				Возврат 12;
			КонецЕсли;
			
		Иначе
			СтрокаСоответствия = ПолучитьЭлементСоответствияПоИмениДанных(мСоответствиеСтрокДереваИМетаданных, ИмяДанныхРодителя);	
			Возврат СтрокаСоответствия.НомерКартинкиКонечногоЭлемента;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

//Функция возвращает строку дерева с такими же метаданными
Функция ВернутьСтрокуДереваПоМетаданным(ДеревоПоиска, МетаданныеОбъекта)
	
	СтрокаПоиска = ДеревоПоиска.Строки.Найти(МетаданныеОбъекта, "Данные", Ложь);
	
	Возврат СтрокаПоиска;
	
КонецФункции

Функция ПолучитьПоМетаданномуНазвание(МетаданныеОбъекта)
	
	НазваниеМетаданных = "";
	
	Если Метаданные.Справочники.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "Справочник";
		
	ИначеЕсли Метаданные.Документы.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "Документ";
		
	ИначеЕсли Метаданные.ПланыВидовХарактеристик.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "ПланВидовХарактеристик";
		
	ИначеЕсли Метаданные.ПланыСчетов.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "ПланСчетов";
		
	ИначеЕсли Метаданные.РегистрыСведений.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "РегистрСведений";
		
	ИначеЕсли Метаданные.ПланыВидовРасчета.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "ПланВидовРасчета";
		
	ИначеЕсли Метаданные.РегистрыНакопления.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "РегистрНакопления";
		
	ИначеЕсли Метаданные.РегистрыБухгалтерии.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "РегистрБухгалтерии";
		
	ИначеЕсли Метаданные.РегистрыРасчета.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "РегистрРасчета";
		
	ИначеЕсли Метаданные.БизнесПроцессы.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "БизнесПроцесс";
		
	ИначеЕсли Метаданные.Задачи.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "Задача";
					   
	ИначеЕсли Метаданные.Константы.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "Константа";
		
	ИначеЕсли Метаданные.Последовательности.Содержит(МетаданныеОбъекта) Тогда
		
		НазваниеМетаданных = "Последовательность";
		
	Иначе
		
		НазваниеМетаданных = "";
				
	КонецЕсли;
	
	Возврат НазваниеМетаданных;
	
КонецФункции

Функция ПолучитьКоличествоЗарегистрированныхОбъектовДляМетаданногоПоУзлу(МетаданныеОбъекта)
	
	Запрос = Новый Запрос();
	ИмяТаблицыДляПоискаЗарегистрированных = ПолучитьПоМетаданномуНазвание(МетаданныеОбъекта);
	Если ПустаяСтрока(ИмяТаблицыДляПоискаЗарегистрированных) Тогда
		Возврат 0;
	КонецЕсли;
	ИмяТаблицыДляПоискаЗарегистрированных = ИмяТаблицыДляПоискаЗарегистрированных + "." + МетаданныеОбъекта.Имя + ".Изменения";
	Запрос.Текст = "
	   |ВЫБРАТЬ 
	   |    Количество(*) КАК КоличествоЗарегистрированных,
   	   |    СУММА(ВЫБОР КОГДА РегистрацияИзменений.НомерСообщения ЕСТЬ NULL ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КоличествоНевыгруженных,
	   |    СУММА(ВЫБОР КОГДА РегистрацияИзменений.НомерСообщения ЕСТЬ NULL ТОГДА 0 ИНАЧЕ 1 КОНЕЦ) КАК КоличествоВыгруженных
	   |ИЗ
	   |	" + ИмяТаблицыДляПоискаЗарегистрированных + " КАК РегистрацияИзменений
	   |ГДЕ
	   |РегистрацияИзменений.Узел = &Узел";
	Запрос.УстановитьПараметр("Узел", УзелОбмена);
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	Если ТаблицаДанных.Количество() = 0 Тогда
		СтрокаДанных = ТаблицаДанных.Добавить();
	Иначе		
		СтрокаДанных = ТаблицаДанных[0];
	КонецЕсли;
	Возврат СтрокаДанных;
	
КонецФункции

Процедура ОбновитьКоличествоЗарегистрированныхДляСтрокиДерева(СтрокаДерева = Неопределено)
	
	Если СтрокаДерева = Неопределено Тогда
		СтрокаДерева = ЭлементыФормы.ДеревоОбмена.ТекущаяСтрока;
	КонецЕсли; 
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(СтрокаДерева.Данные);
	СтароеКоличествоЗарегистрированныхОбъектов = СтрокаДерева.КоличествоЗарегистрированных;
	СтруктураКоличества = ПолучитьКоличествоЗарегистрированныхОбъектовДляМетаданногоПоУзлу(МетаданныеОбъекта);
	ЗаполнитьЗначенияСвойств(СтрокаДерева, СтруктураКоличества); 
	Родитель = СтрокаДерева.Родитель;
	Пока Родитель <> Неопределено Цикл
		Родитель.КоличествоЗарегистрированных = Родитель.КоличествоЗарегистрированных - СтароеКоличествоЗарегистрированныхОбъектов + СтрокаДерева.КоличествоЗарегистрированных;
		Родитель = Родитель.Родитель;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПостроитьДеревоОбменаДанных(ДеревоОбмена, МетаданныеПланаОбмена, МетаданныеДляОграничения = Неопределено)
	
	Если МетаданныеДляОграничения = Неопределено Тогда
		ДеревоОбмена.Строки.Очистить();
		мСоответствиеСтрокДереваИМетаданных.Очистить();
		
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.Константы, "Константы", "Константы",	0, 0, 0);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.Справочники, "Справочники", "Справочники",	1, 5, 0);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.Документы, "Документы", "Документы",	6, 7, 0);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.Последовательности, "Последовательности", "Последовательности",	26, 26, 26);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.РегистрыСведений, "Регистры сведений", "РегистрыСведений",	2, 2, 3);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.РегистрыНакопления, "Регистры накопления", "РегистрыНакопления",	4, 4, 3);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.ПланыВидовХарактеристик, "Планы видов характеристик", "ПланыВидовХарактеристик",	14, 15, 12);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.ПланыСчетов, "Планы счетов", "ПланыСчетов",	16, 17, 17);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.ПланыВидовРасчета, "Планы видов расчета", "ПланыВидовРасчета",	18, 19, 19);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.РегистрыБухгалтерии, "Регистры бухгалтерии", "РегистрыБухгалтерии",	22, 22, 3);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.РегистрыРасчета, "Регистры расчета", "РегистрыРасчета",	23, 23, 3);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.БизнесПроцессы, "Бизнес-процессы", "БизнесПроцессы",	20, 21, 21);
		ДобавитьМетаданныеДанныеВСтруктуруДерева(мСоответствиеСтрокДереваИМетаданных, Метаданные.Задачи, "Задачи", "Задачи",	24, 24, 25);
		
		МетаданныеОбъекта = Неопределено;
		
		Индикатор = ЛксПолучитьИндикаторПроцесса(МетаданныеПланаОбмена.Состав.Количество(), "Подготовка данных");
		// надо определить что подлежит обмену
		Для Каждого СтрокаДляОбмена Из МетаданныеПланаОбмена.Состав Цикл
			ЛксОбработатьИндикатор(Индикатор);
			МетаданныеОбъектаПредка = МетаданныеОбъекта;
			МетаданныеОбъекта = СтрокаДляОбмена.Метаданные;
			Если МетаданныеОбъекта = Неопределено Тогда
				// Это сама конфигурация
				Продолжить;
			КонецЕсли;
			ЭлементСоответствия = ПолучитьЭлементСоответствияПоМетаданным(мСоответствиеСтрокДереваИМетаданных, МетаданныеОбъекта);
			Если ЭлементСоответствия = Неопределено Тогда
				// перерасчеты не отображаем в обработке
				//СообщитьОбОшибкеИис("Невозможно определить тип объекта: " + Строка(МетаданныеОбъекта));
				Продолжить;
			КонецЕсли;
			СтрокаДерева = ЭлементСоответствия.СтрокаДерева;
			СтрокаДобавления = СтрокаДерева.Строки.Добавить(); 
			СтрокаДобавления.Имя = МетаданныеОбъекта.Имя;
			СтрокаДобавления.Представление = МетаданныеОбъекта.Представление();
			СтрокаДобавления.Данные = МетаданныеОбъекта.ПолноеИмя();
			СтрокаДобавления.РежимАвтоРегистрации = (СтрокаДляОбмена.АвтоРегистрация = АвтоРегистрацияИзменений.Разрешить);
			СтрокаДобавления.КоличествоЗарегистрированных = 0;
		КонецЦикла;
		ЛксОсвободитьИндикаторПроцесса();
		СтрокиКУдалению = Новый Массив;
		Для Каждого СтрокаТипаМетаданных Из ДеревоОбмена.Строки Цикл
			Если СтрокаТипаМетаданных.Строки.Количество() = 0 Тогда
				СтрокиКУдалению.Добавить(СтрокаТипаМетаданных);
			КонецЕсли; 
		КонецЦикла;
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			ДеревоОбмена.Строки.Удалить(СтрокаКУдалению);
		КонецЦикла;
	Иначе
		ЭлементыФормы.ДеревоОбмена.ТекущиеДанные.Строки.Очистить();
	КонецЕсли;

	Если ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения Тогда
		ирНеглобальный.ОбновитьКоличествоСтрокТаблицВДеревеМетаданныхЛкс(ДеревоОбмена, "Данные", "КоличествоЗарегистрированных", Истина,
			Новый Структура("Узел", УзелОбмена));
		СтрокиКУдалению = Новый Массив();
		Для Каждого СтрокаДерева1 Из ДеревоОбмена.Строки Цикл
			Для Каждого СтрокаДерева2 Из СтрокаДерева1.Строки Цикл
				Если Истина
					И ОтображатьТолькоМетаданныеСИзменениями 
					И СтрокаДерева2.КоличествоЗарегистрированных = 0
				Тогда
					СтрокиКУдалению.Добавить(СтрокаДерева2);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			СтрокаКУдалению.Родитель.Строки.Удалить(СтрокаКУдалению);
		КонецЦикла;
		
		СтрокиКУдалению = Новый Массив();
		Для Каждого СтрокаДерева1 Из ДеревоОбмена.Строки Цикл
			Если СтрокаДерева1.Строки.Количество() = 0 Тогда
				СтрокиКУдалению.Добавить(СтрокаДерева1);
			КонецЕсли;
		КонецЦикла;
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			ДеревоОбмена.Строки.Удалить(СтрокаКУдалению);
		КонецЦикла;
	КонецЕсли;
	    	
КонецПроцедуры

//Функция ОбновитьКоличествоЗарегистрированныхОбъектовДляВсехМетаданныхПоУзлу(ИмяКлючевойКолонки = "Данные", ИмяКолонкиКоличества = "КоличествоЗарегистрированных")
//	
//	ТекстПакета = "";
//	ТекстЗапроса = "";
//	Для Каждого СтрокаДерева1 Из ДеревоОбмена.Строки Цикл
//		Для Каждого СтрокаДерева2 Из СтрокаДерева1.Строки Цикл
//			Если ТекстЗапроса <> "" Тогда
//				ТекстЗапроса = ТекстЗапроса + "
//				|ОБЪЕДИНИТЬ ВСЕ";
//			КонецЕсли; 
//			ТекстЗапроса = ТекстЗапроса + "
//			|ВЫБРАТЬ
//			|	""" + СтрокаДерева2[ИмяКлючевойКолонки] + """ КАК " + ИмяКлючевойКолонки + ",
//			|    Количество(*) КАК " + ИмяКолонкиКоличества + ",
//			|    СУММА(ВЫБОР КОГДА Т.НомерСообщения ЕСТЬ NULL ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КоличествоНевыгруженных,
//			|    СУММА(ВЫБОР КОГДА Т.НомерСообщения ЕСТЬ NULL ТОГДА 0 ИНАЧЕ 1 КОНЕЦ) КАК КоличествоВыгруженных
//			|ИЗ " + ирНеглобальный.ПолучитьИмяТаблицыИзМетаданныхЛкс(СтрокаДерева2.Данные + ".Изменения") + " КАК Т
//			|ГДЕ
//			|Т.Узел = &Узел";
//		КонецЦикла;
//		СчетчикТаблиц = СчетчикТаблиц + 1;
//		Если СчетчикТаблиц = 255 Тогда
//			Если ТекстПакета <> "" Тогда
//				ТекстПакета = ТекстПакета + "
//				|;";
//			КонецЕсли; 
//			ТекстПакета = ТекстПакета + ТекстЗапроса;
//			ТекстЗапроса = "";
//		КонецЕсли; 
//	КонецЦикла;
//	Запрос = Новый Запрос;
//	Запрос.Текст = ТекстПакета;
//	Запрос.УстановитьПараметр("Узел", УзелОбмена);
//	РезультатПакета = Запрос.ВыполнитьПакет();
//	Для Каждого РезультатЗапроса Из РезультатПакета Цикл
//		ТаблицаРезультата = РезультатЗапроса.Выгрузить();
//		Для Каждого СтрокаРезультата Из ТаблицаРезультата Цикл
//			СтрокаДерева = ДеревоОбмена.Строки.Найти(СтрокаРезультата[ИмяКлючевойКолонки], ИмяКлючевойКолонки, Истина);
//			Если СтрокаДерева <> Неопределено Тогда
//				СтароеКоличество = СтрокаДерева[ИмяКолонкиКоличества];
//				ЗаполнитьЗначенияСвойств(СтрокаДерева, СтрокаРезультата); 
//				Родитель = СтрокаДерева.Родитель;
//				Пока Родитель <> Неопределено Цикл
//					Родитель[ИмяКолонкиКоличества] = Родитель[ИмяКолонкиКоличества] - СтароеКоличество + СтрокаДерева[ИмяКолонкиКоличества];
//					Родитель = Родитель.Родитель;
//				КонецЦикла;
//			КонецЕсли; 
//		КонецЦикла;
//	КонецЦикла; 
//	
//КонецФункции

//Функция по имени родителя предка формируется префикс типа
Функция СформироватьПрефиксТипаСсылки(Знач НаименованиеПредка)
	
	Если НаименованиеПредка = "Справочники" Тогда
		Возврат "СправочникСсылка";
	ИначеЕсли НаименованиеПредка = "Документы" Тогда
		Возврат "ДокументСсылка";
	ИначеЕсли НаименованиеПредка = "ПланыВидовХарактеристик" Тогда
		Возврат "ПланВидовХарактеристикСсылка";
	ИначеЕсли НаименованиеПредка = "ПланыСчетов" Тогда
		Возврат "ПланСчетовСсылка";
	ИначеЕсли НаименованиеПредка = "ПланыВидовРасчета" Тогда
		Возврат "ПланВидовРасчетаСсылка";
	ИначеЕсли НаименованиеПредка = "БизнесПроцессы" Тогда
		Возврат "БизнесПроцессСсылка";
	ИначеЕсли НаименованиеПредка = "Задачи" Тогда
		Возврат "ЗадачаСсылка";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

//процедура строит дерево измененных объектов для узла
Процедура ПостроитьДеревоИзмененнныхОбъектовДляУзла(Знач МетаданныеДляОграничения = Неопределено)
	
	Если ТипЗнч(МетаданныеДляОграничения) = Тип("Строка") Тогда
		МетаданныеДляОграничения = Метаданные.НайтиПоПолномуИмени(МетаданныеДляОграничения);
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(УзелОбмена) Тогда
		Предупреждение("Не указан узел обмена");
		Возврат;
	КонецЕсли;
	
	ПутьКСтаройСтроке = "";
	ТекущаяСтрока = ЭлементыФормы.ДеревоОбмена.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		ПутьКСтаройСтроке = ТекущаяСтрока.Данные;
	КонецЕсли; 
	МетаданныеПланаОбмена = УзелОбмена.Метаданные();
	
	// надо построить дерево тех метаданных которые учавствуют в обмене
	ПостроитьДеревоОбменаДанных(ДеревоОбмена, МетаданныеПланаОбмена, МетаданныеДляОграничения);
	
	Если МетаданныеДляОграничения = Неопределено Тогда
		ДеревоОбмена.Строки.Сортировать("Имя", Истина);
	КонецЕсли;
	Если ПутьКСтаройСтроке <> "" Тогда
		ТекущаяСтрока = ДеревоОбмена.Строки.Найти(ПутьКСтаройСтроке, "Данные", Истина);
		Если ТекущаяСтрока <> Неопределено Тогда
			ЭлементыФормы.ДеревоОбмена.ТекущаяСтрока = ТекущаяСтрока;
		КонецЕсли; 
	КонецЕсли; 
	Если ЭлементыФормы.ДеревоОбмена.ТекущаяСтрока = Неопределено Тогда
		КП_ТаблицаИзмененийОбновить();
	КонецЕсли; 
	
КонецПроцедуры

//процедура добавляет/удаляет регистрацию для объектов
Процедура ДобавитьУдалитьРегистрациюДляОбъектов(Знач ДобавитьРегистрацию = Истина)
	
	Если ЭлементыФормы.ДеревоОбмена.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УровеньВыбраннойСтроки = ЭлементыФормы.ДеревоОбмена.ТекущиеДанные.Уровень();
	РегистрацияВсехИзменений = Истина;
	СтрокаДобавленияУдаленияРегистрации = ?(ДобавитьРегистрацию, "Добавить", "Удалить");
	
	СписокРедактирования = Неопределено;
	НужноОбновлятьДерево = ДобавитьРегистрацию;
	
	Если УровеньВыбраннойСтроки = 0 Тогда
		
		// общий класс, справочники, константы...
		ТекстПодтверждения = СтрокаДобавленияУдаленияРегистрации + " регистрацию для всех объектов с типом: " + ЭлементыФормы.ДеревоОбмена.ТекущиеДанные.Имя;
		ОтветПользователя = Вопрос(ТекстПодтверждения, РежимДиалогаВопрос.ДаНет);
		Если ОтветПользователя <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
	ИначеЕсли УровеньВыбраннойСтроки = 1 Тогда
		
		// конкретный справочник или константа
		ТекстПодтверждения = СтрокаДобавленияУдаленияРегистрации + " регистрацию для всех (Да) или выборочных (Нет) объектов с типом: " + ЭлементыФормы.ДеревоОбмена.ТекущиеДанные.Имя;
		
		ОтветПользователя = Вопрос(ТекстПодтверждения, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Нет);
		Если ОтветПользователя = КодВозвратаДиалога.Отмена Тогда
			Возврат;
			
		ИначеЕсли ОтветПользователя = КодВозвратаДиалога.Да Тогда
			РегистрацияВсехИзменений = Истина;
			
		ИначеЕсли ОтветПользователя = КодВозвратаДиалога.Нет Тогда
			РегистрацияВсехИзменений = Ложь;
			СписокРедактирования = ПолучитьДанныеДляРегистрацииИзменений(ЭлементыФормы.ДеревоОбмена.ТекущиеДанные.Данные);
			НужноОбновлятьДерево = Истина;
			
		КонецЕсли;
				
	//ИначеЕсли УровеньВыбраннойСтроки = 2 Тогда	
	//	
	//	Если ДобавитьРегистрацию Тогда 
	//		// конкретный элемент...
	//		СписокРедактирования = ПолучитьДанныеДляРегистрацииИзменений(ЭлементыФормы.ДеревоОбмена.ТекущиеДанные.Родитель.Данные);
	//	Иначе
	//		СписокРедактирования = Новый СписокЗначений;
	//		СписокРедактирования.Добавить(ЭлементыФормы.ДеревоОбмена.ТекущиеДанные.Данные);
	//	КонецЕсли;
	//	
	КонецЕсли;
	
	СтрокаДереваТекущихДанных = ЭлементыФормы.ДеревоОбмена.ТекущиеДанные;
	Если УровеньВыбраннойСтроки = 0 Тогда
		Для Каждого СтрокаДерева Из СтрокаДереваТекущихДанных.Строки Цикл
			ОбъектМеданных = Метаданные.НайтиПоПолномуИмени(СтрокаДерева.Данные);
			Если ДобавитьРегистрацию Тогда
				ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, ОбъектМеданных);
			Иначе 
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, ОбъектМеданных);
				УдалитьКонечныеЭлементыДерева(ЭлементыФормы.ДеревоОбмена.ТекущиеДанные);
			КонецЕсли;
			ОбновитьКоличествоЗарегистрированныхДляСтрокиДерева(СтрокаДерева);
		КонецЦикла;
					
	ИначеЕсли Истина
		И УровеньВыбраннойСтроки = 1 
		И РегистрацияВсехИзменений
	Тогда
		ОбъектМеданных = Метаданные.НайтиПоПолномуИмени(СтрокаДереваТекущихДанных.Данные);
		Если ДобавитьРегистрацию Тогда
			ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, ОбъектМеданных);
		Иначе
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, ОбъектМеданных);
			УдалитьКонечныеЭлементыДерева(ЭлементыФормы.ДеревоОбмена.ТекущиеДанные);
		КонецЕсли;
		ОбновитьКоличествоЗарегистрированныхДляСтрокиДерева(СтрокаДереваТекущихДанных);
	Иначе
		// регистрация того, что выберет пользователь
		Если СписокРедактирования = Неопределено Тогда
			// ничего не смогли поделать
			Возврат;
		КонецЕсли;
		
		СтрокаОбновления = ЭлементыФормы.ДеревоОбмена.ТекущиеДанные;
		Если СтрокаОбновления.Уровень() >= 2 Тогда
			СтрокаОбновления = СтрокаОбновления.Родитель;
		КонецЕсли;
			
		Для Каждого ЗначенияСписка Из СписокРедактирования Цикл
			
			//Попытка
			//	
			//	Если НЕ ЗначениеЗаполнено(ЗначенияСписка.Значение) Тогда
			//		Продолжить;
			//	КонецЕсли;
			//	
			//Исключение
			//КонецПопытки;
				
			Если ДобавитьРегистрацию Тогда
				ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, ЗначенияСписка.Значение);
			Иначе
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, ЗначенияСписка.Значение);
					
				Если Не НужноОбновлятьДерево Тогда
					ЭлементыФормы.ДеревоОбмена.ТекущиеДанные.Родитель.Строки.Удалить(ЭлементыФормы.ДеревоОбмена.ТекущиеДанные);	
				КонецЕсли;
					
			КонецЕсли;
					
		КонецЦикла;
		
		ОбновитьКоличествоЗарегистрированныхДляСтрокиДерева(СтрокаОбновления);
				
	КонецЕсли;
	
	Если НужноОбновлятьДерево Тогда
		ПостроитьДеревоИзмененнныхОбъектовДляУзла();
	Иначе
		КП_ТаблицаИзмененийОбновить();
	КонецЕсли;
	
КонецПроцедуры

	
// для узла обмена показывает зарегистрированные изменения
Процедура КоманднаяПанельДереваОбъектовОбновить(Кнопка)
	
	ПостроитьДеревоИзмененнныхОбъектовДляУзла();
		
КонецПроцедуры

// при выводе строки дерева
Процедура ДеревоОбменаПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Имя.ОтображатьКартинку = истина;
	ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.ОтображатьКартинку = Ложь;
	ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = "";
		
	УровеньДерева = ДанныеСтроки.Уровень();
	
	ОформлениеСтроки.Ячейки.Имя.ИндексКартинки = ПолучитьНомерКартинкиПоДанным(УровеньДерева, ДанныеСтроки);
	
	Если УровеньДерева = 1 Тогда
		
		ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.ОтображатьКартинку = Истина;
		
		Если ДанныеСтроки.РежимАвтоРегистрации = Истина Тогда
			
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.ИндексКартинки = 0;
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = "Разрешить";
			
		Иначе
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.ИндексКартинки = 1;
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = "Запретить";
			
		КонецЕсли;
		
		ОформлениеСтроки.Ячейки.Имя.ИндексКартинки = ПолучитьНомерКартинкиПоДанным(УровеньДерева, ДанныеСтроки);
		
		Если ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения
			И ДанныеСтроки.КоличествоЗарегистрированных <> 0 Тогда
			
			//ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст + " (" + Строка(ДанныеСтроки.КоличествоЗарегистрированных) + ")";	
			ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст;
			
		КонецЕсли;
		
	ИначеЕсли УровеньДерева = 0 Тогда
		
		//Если ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения
		//	И ДанныеСтроки.КоличествоЗарегистрированных <> 0 Тогда
		//	
		//	ОформлениеСтроки.Ячейки.РежимАвтоРегистрации.Текст = "Зарегистрировано: " + Строка(ДанныеСтроки.КоличествоЗарегистрированных);	
		//	
		//КонецЕсли;
			
	
		
	КонецЕсли;
	
КонецПроцедуры

// развернуть дерево
Процедура КоманднаяПанельДереваОбъектовРазвернуть(Кнопка)
	
	Если ЭлементыФормы.ДеревоОбмена.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы.ДеревоОбмена.Развернуть(ЭлементыФормы.ДеревоОбмена.ТекущиеДанные, Истина);
	
КонецПроцедуры

// свернуть дерево
Процедура КоманднаяПанельДереваОбъектовСвернутьДерево(Кнопка)

	Если ЭлементыФормы.ДеревоОбмена.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы.ДеревоОбмена.Свернуть(ЭлементыФормы.ДеревоОбмена.ТекущиеДанные);
	
КонецПроцедуры

// процедура отображает данные регитсра
Процедура ОтобразитьДанныеРегистра(Данные, Знач ИмяГлавногоРодителя)
	
	СтрокаТипа = Строка(Данные);
	
	ПозицияТочки = Найти(СтрокаТипа, ".");
	
	ИмяРегистра = Сред(СтрокаТипа, ПозицияТочки + 1);
	
	Если ИмяГлавногоРодителя = "РегистрыСведений" Тогда
	
		ФормаСписка = РегистрыСведений[ИмяРегистра].ПолучитьФормуСписка();
	
	ИначеЕсли ИмяГлавногоРодителя = "РегистрыНакопления" Тогда
		
		ФормаСписка = РегистрыНакопления[ИмяРегистра].ПолучитьФормуСписка();
		
	ИначеЕсли ИмяГлавногоРодителя = "РегистрыБухгалтерии" Тогда
		
		ФормаСписка = РегистрыБухгалтерии[ИмяРегистра].ПолучитьФормуСписка();
		
	ИначеЕсли ИмяГлавногоРодителя = "РегистрыРасчета" Тогда
		
		ФормаСписка = РегистрыРасчета[ИмяРегистра].ПолучитьФормуСписка();	
		
	КонецЕсли;
	
	// отбор ставим
	Для Каждого СтрокаОтбора Из Данные.Отбор Цикл
		
		СтрокаОтбораФормы = ФормаСписка.Отбор.Найти(СтрокаОтбора.Имя);
		
		Если СтрокаОтбораФормы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаОтбораФормы.Использование = СтрокаОтбора.Использование;
		СтрокаОтбораФормы.ВидСравнения = СтрокаОтбора.ВидСравнения;
		СтрокаОтбораФормы.Значение = СтрокаОтбора.Значение;
		
	КонецЦикла;
	
	ФормаСписка.Открыть();
	
КонецПроцедуры

// выбор строки дерева
Процедура ДеревоОбменаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УровеньВыбраннойСтроки = ВыбраннаяСтрока.Уровень();
	
	Если УровеньВыбраннойСтроки < 1 Тогда
		Элемент.Развернуть(ВыбраннаяСтрока);
		Возврат;
	КонецЕсли;
	Если УровеньВыбраннойСтроки = 1 Тогда
		ОбновитьКоличествоЗарегистрированныхДляСтрокиДерева(ВыбраннаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

//Процедура удаляет все строки дерева у которых уровень не меньше 2
Процедура УдалитьКонечныеЭлементыДерева(ДеревоДанных)
	
	Если ДеревоДанных.Уровень() >= 1 Тогда
		
		ДеревоДанных.Строки.Очистить();
		
	Иначе
		
		Для Каждого СтрокаДерева Из ДеревоДанных.Строки Цикл
			
			УдалитьКонечныеЭлементыДерева(СтрокаДерева);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// удаление регистрации изменений
Процедура КоманднаяПанельДереваОбъектовОтменитьРегистрациюИзменения(Кнопка)
	
	ДобавитьУдалитьРегистрациюДляОбъектов(Ложь);
		
КонецПроцедуры

//Функция возвращает данные для регистрации изменений
Функция ПолучитьДанныеДляРегистрацииИзменений(ПолноеИмяМД)
	
	// не для всех типов позволяем список объектов для регистрации редактировать
	Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
		ФормаРедактированияОбъектов = ПолучитьФорму("ФормаВыбораОбъектовДляРегистрации", ЭтаФорма);
		МассивТипов = Новый Массив();
		МассивТипов.Добавить(Тип(СтрЗаменить(ПолноеИмяМД, ".", "Ссылка.")));
		ФормаРедактированияОбъектов.ОписаниеТиповРедактированияСписка = Новый ОписаниеТипов(МассивТипов);
		РезультатОткрытия = ФормаРедактированияОбъектов.ОткрытьМодально();
		Если РезультатОткрытия <> Истина Тогда
			Возврат Неопределено;
		КонецЕсли;
		СписокРедактирования = ФормаРедактированияОбъектов.СписокРедактирования;
		
		// Избавляемся от пустых ссылок, т.к. они приводят к глобальной регистрации всех данных
		НачальноеКоличество = СписокРедактирования.Количество(); 
		Для СчетчикСписокРедактирования = 1 По НачальноеКоличество Цикл
			ЭлементСписка = СписокРедактирования[НачальноеКоличество - СчетчикСписокРедактирования];
			Если Не ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
				СписокРедактирования.Удалить(ЭлементСписка);
			КонецЕсли;
		КонецЦикла;
		
		// ничего не выбрали для регистрации
		Если СписокРедактирования.Количество() = 0 Тогда 
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;	

	Возврат СписокРедактирования;
	
КонецФункции

// добавить регистрацию
Процедура КоманднаяПанельДереваОбъектовДобавитьРегистрацию(Кнопка)
	
	ДобавитьУдалитьРегистрациюДляОбъектов(Истина);
	
КонецПроцедуры

//Функция регистрирует изменнения для всего дерева объектов
Функция ИзменитьРегистрациюИзмененийНаУзлеДляВсегоСоставаПланаОбмена(НовоеЗначение = Истина)
	
	Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
		Предупреждение("Сначала необходимо выбрать узел");
		Возврат Ложь;
	КонецЕсли; 
	ФормаВопроса = ПолучитьФорму("ФормаВыбораОбновленияРегистрации", ЭтаФорма);
	Если НовоеЗначение Тогда
		ФормаВопроса.ТекстВопросаПользователю = "Операция регистрации всех изменений необратима, Вы уверены, что хотите зарегистрировать изменения для всех объектов участвующих в обмене?";
	Иначе
		ФормаВопроса.ТекстВопросаПользователю = "Операция удаления регистрации изменений необратима, Вы уверены, что хотите удалить регистрацию изменений для всех объектов участвующих в обмене?";
	КонецЕсли; 
	РезультатОткрытия = ФормаВопроса.ОткрытьМодально();
	Если РезультатОткрытия <> Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТолькоДляОбъектовСАвтоматическойРегистрацией = ФормаВопроса.ОбновлятьТолькоДляЭлементовСАвтоРегистрацией;
	МетаданныеПланаОбмена = УзелОбмена.Метаданные();
	Индикатор = ЛксПолучитьИндикаторПроцесса(МетаданныеПланаОбмена.Состав.Количество(), "Регистрация изменени");
	Для Каждого ЭлементСостава Из МетаданныеПланаОбмена.Состав Цикл
		ЛксОбработатьИндикатор(Индикатор);
		ОбъектМеданных = ЭлементСостава.Метаданные;
		Если ОбъектМеданных = Неопределено Тогда
			// Это сама конфигурация
			Продолжить;
		КонецЕсли;
		Если Истина
			И ТолькоДляОбъектовСАвтоматическойРегистрацией
			И ЭлементСостава.АвтоРегистрация = АвтоРегистрацияИзменений.Запретить 
		Тогда
			Продолжить;
		КонецЕсли;
		Если НовоеЗначение Тогда
			ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, ОбъектМеданных);
		Иначе
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, ОбъектМеданных);
		КонецЕсли; 
	КонецЦикла;
	ЛксОсвободитьИндикаторПроцесса();
	Возврат Истина;
	
КонецФункции

Функция ПолучитьОбъектДанныхПоСтрокеИзменений(СтрокаДанных = Неопределено)
	
	Если СтрокаДанных = Неопределено Тогда
		СтрокаДанных = ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока;
	КонецЕсли; 
	Если СтрокаДанных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли; 
	Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
		Объект = СтрокаДанных.Ссылка.ПолучитьОбъект();
		Если Объект = Неопределено Тогда
			Объект = Новый УдалениеОбъекта(СтрокаДанных.Ссылка);
		КонецЕсли; 
	ИначеЕсли мТекущаяГруппаТипаМетаданных = "Регистр" Тогда
		Объект = мМакетныйОбъект;
		// Антибаг 8.2.15 http://www.partners.v8.1c.ru/forum/thread.jsp?id=1034617#1034617
		//Объект.Отбор.Сбросить();
		Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
			Если Колонка.Имя = "НомерСообщения" Тогда
				Продолжить;
			КонецЕсли; 
			ЭлементОтбора = мМакетныйОбъект.Отбор[Колонка.Имя];
			ЛксПрисвоитьЕслиНеРавно(ЭлементОтбора.Использование, Истина);
			ЭлементОтбора.Значение = СтрокаДанных[Колонка.Имя];
		КонецЦикла;
		Объект.Прочитать();
	ИначеЕсли мТекущаяГруппаТипаМетаданных = "Константа" Тогда
		Объект = мМакетныйОбъект;
		Объект.Прочитать();
	КонецЕсли;
	Возврат Объект;
	
КонецФункции


// показываем результат стандарной выгрузки
Процедура КоманднаяПанельДереваОбъектовПоказатьРезультатСтандартнойВыгрузки(Кнопка)
	
	Если ЭлементыФормы.ТаблицаИзменений.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-16");
	Объект = ПолучитьОбъектДанныхПоСтрокеИзменений();
	Попытка
		ЗаписатьXML(ЗаписьXML, Объект); 
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	СтрXML = ЗаписьXML.Закрыть();
	ирНеглобальный.ОткрытьТекстЛкс(СтрXML, "XML представление объекта """ + ЭлементыФормы.ДеревоОбмена.ТекущаяСтрока.Данные + "." + Объект + """", "HTML", Истина, ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока);
	
КонецПроцедуры

Процедура ВыполнитьРегистрациюПервогоОбъектаДляУзла(Знач ПрефиксОбъектаМетаданных, Знач НазваниеОбъекта)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ Первые 1
	               |	ОбъектДанных.Ссылка
	               |ИЗ
	               |	" + ПрефиксОбъектаМетаданных + "." + НазваниеОбъекта + " КАК ОбъектДанных";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, Выборка.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьРегистрациюРегистра(Знач ИмяТипаОбъектаМетаданных, Знач ПрефиксОбъектаМетаданных, Знач НазваниеОбъекта)
	
	КолонкаОтбора = "Регистратор";
	
	Если ИмяТипаОбъектаМетаданных = "РегистрыСведений" Тогда
		
		МетаданныеРегистра = Метаданные.РегистрыСведений[НазваниеОбъекта];
		
		Если МетаданныеРегистра.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
			
			КолонкаОтбора = "";
			МассивКолонокОтбора = Новый Массив();
			
			Если МетаданныеРегистра.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
				
				МассивКолонокОтбора.Добавить("Период");		
				
			КонецЕсли;
			
			Для Каждого КолонкаИзмерений Из МетаданныеРегистра.Измерения Цикл
				
				Если КолонкаИзмерений.ОсновнойОтбор = Истина Тогда
					
					МассивКолонокОтбора.Добавить(КолонкаИзмерений.Имя);	
					
				КонецЕсли;
											
			КонецЦикла;
			
		КонецЕсли;
						
	КонецЕсли;		
	
	Запрос = Новый Запрос();
	
	СтрокаКолонокВыборкиДанных = "";
	
	Если КолонкаОтбора <> "" Тогда
		
		СтрокаКолонокВыборкиДанных = "ОбъектДанных." + КолонкаОтбора;
		
	Иначе
		
		Для Каждого ЭлементКолонки Из МассивКолонокОтбора Цикл
			
			СтрокаКолонокВыборкиДанных = СтрокаКолонокВыборкиДанных + "," + Символы.ПС +  "ОбъектДанных." + ЭлементКолонки;
			
		КонецЦикла;
		
		СтрокаКолонокВыборкиДанных = СокрЛП(Сред(СтрокаКолонокВыборкиДанных, 2));
		
	КонецЕсли;
	
	
	// нет ни измерений, ни периодичности
	Если ПустаяСтрока(СтрокаКолонокВыборкиДанных) Тогда
		
		ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, МетаданныеРегистра);	
		Возврат;
		
	КонецЕсли;
	
	
	Запрос.Текст = "ВЫБРАТЬ Первые 1
				   |	" + СтрокаКолонокВыборкиДанных + " 
				   |ИЗ
				   |	" + ПрефиксОбъектаМетаданных + "." + НазваниеОбъекта + " КАК ОбъектДанных";
				   
				   
					   
	Выборка = Запрос.Выполнить().Выбрать();
		
	Если Выборка.Следующий() Тогда
		
		Если ИмяТипаОбъектаМетаданных = "Последовательности" Тогда
			
			НаборЗаписей = Последовательности[НазваниеОбъекта].СоздатьНаборЗаписей();
			
		ИначеЕсли ИмяТипаОбъектаМетаданных = "РегистрыСведений" Тогда
			
			НаборЗаписей = РегистрыСведений[НазваниеОбъекта].СоздатьНаборЗаписей();
			
		ИначеЕсли ИмяТипаОбъектаМетаданных = "РегистрыНакопления" Тогда	
			
			НаборЗаписей = РегистрыНакопления[НазваниеОбъекта].СоздатьНаборЗаписей();
			
		ИначеЕсли ИмяТипаОбъектаМетаданных = "РегистрыБухгалтерии" Тогда	
			
			НаборЗаписей = РегистрыБухгалтерии[НазваниеОбъекта].СоздатьНаборЗаписей();	
			
		ИначеЕсли ИмяТипаОбъектаМетаданных = "РегистрыРасчета" Тогда	
			
			НаборЗаписей = РегистрыРасчета[НазваниеОбъекта].СоздатьНаборЗаписей();	
				
		КонецЕсли;
		
		Если КолонкаОтбора <> "" Тогда 
			
			НаборЗаписей.Отбор[КолонкаОтбора].Установить(Выборка[КолонкаОтбора]);
			
		Иначе
			
			Для Каждого ЭлементКолонки Из МассивКолонокОтбора Цикл
			
				НаборЗаписей.Отбор[ЭлементКолонки].Установить(Выборка[ЭлементКолонки]);
			
			КонецЦикла;
			
		КонецЕсли;
		
		
		ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, НаборЗаписей);
			
	КонецЕсли;
	
КонецПроцедуры

Функция ВыбратьУзелОбменаПоМетаданным(МетаданныеПланаОбмена)
	
	ИмяПланаОбмена = МетаданныеПланаОбмена.Имя;
	
	Запрос = Новый запрос();
	Запрос.Текст = "ВЫБРАТЬ первые 2
	               |	ПланОбменаРезультат.Ссылка
	               |ИЗ
	               |	ПланОбмена." + ИмяПланаОбмена + " КАК ПланОбменаРезультат
	               |ГДЕ
	               |	ПланОбменаРезультат.ПометкаУдаления = Ложь
	               |	И ПланОбменаРезультат.Ссылка <> &ТекущийУзелОбмена";
				   
	Запрос.УстановитьПараметр("ТекущийУзелОбмена", ПланыОбмена[ИмяПланаОбмена].ЭтотУзел());
	
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаРезультата.Количество() = 1 Тогда
		
		Возврат ТаблицаРезультата[0].Ссылка;
		
	Иначе
		
		Возврат ПланыОбмена[ИмяПланаОбмена].ПустаяСсылка();
		
	КонецЕсли;
	
КонецФункции

Процедура УзелОбменаПриИзменении(Элемент = Неопределено) Экспорт
	
	Если Элемент <> Неопределено Тогда
		// Антибаг платформы 8.2.16 http://partners.v8.1c.ru/forum/thread.jsp?id=1077270#1077270
		Элемент.Значение = Элемент.Значение;
	КонецЕсли; 
	
	Если Элемент = Неопределено Тогда
		Элемент = ЭлементыФормы.УзелОбмена;
	КонецЕсли; 
	ДеревоОбмена.Строки.Очистить();
	КП_ТаблицаИзмененийОбновить();
	ЭлементыФормы.ТаблицаИзменений.ТолькоПросмотр = Истина;
	Если УзелОбмена = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(УзелОбмена) Тогда
		// возможно тип то уже определен, тогда достаточно будет по типу определить план обмена и если в плане 2 элемента то взять не текущий и все
		МетаданныеПланаОбмена = УзелОбмена.Метаданные();
		УзелОбмена = ВыбратьУзелОбменаПоМетаданным(МетаданныеПланаОбмена);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(УзелОбмена) Тогда
		Возврат;
	КонецЕсли;
	ирНеглобальный.ПолеВводаСИсториейВыбора_ПриИзмененииЛкс(Элемент, Метаданные().Имя);
	ПостроитьДеревоИзмененнныхОбъектовДляУзла();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ЗначениеЗаполнено(ПараметрУзелОбмена) Тогда
		УзелОбмена = ПараметрУзелОбмена;
		ПараметрУзелОбмена = Неопределено;
	КонецЕсли; 
	УзелОбменаПриИзменении();
	//Если НЕ ЗначениеЗаполнено(УзелОбмена) Тогда
	//	Возврат;
	//КонецЕсли;
	//ПостроитьДеревоИзмененнныхОбъектовДляУзла();
	
КонецПроцедуры

Процедура УдалитьРегистрациюДляВсехОбъектовНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
		Предупреждение("Сначала необходимо выбрать узел");
		Возврат;
	КонецЕсли; 
	РезультатРегистрации = ИзменитьРегистрациюИзмененийНаУзлеДляВсегоСоставаПланаОбмена(Ложь);
	Если РезультатРегистрации Тогда
	     ПостроитьДеревоИзмененнныхОбъектовДляУзла();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзмененияДляВсехОбъектовНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
		Предупреждение("Сначала необходимо выбрать узел");
		Возврат;
	КонецЕсли; 
	РезультатРегистрации = ИзменитьРегистрациюИзмененийНаУзлеДляВсегоСоставаПланаОбмена(Истина);
	Если РезультатРегистрации Тогда
	     ПостроитьДеревоИзмененнныхОбъектовДляУзла();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзмененияПоОдномуОбъектуНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
		Предупреждение("Сначала необходимо выбрать узел");
		Возврат;
	КонецЕсли; 
	ФормаВопроса = ПолучитьФорму("ФормаВыбораОбновленияРегистрации", ЭтаФорма);
	ФормаВопроса.ТекстВопросаПользователю = "Операция регистрации одного изменения для каждого типа объектов необратима, Вы уверены, что хотите зарегистрировать изменения?";
				
	РезультатОткрытия = ФормаВопроса.ОткрытьМодально();
				
	Если РезультатОткрытия <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ТолькоДляОбъектовСАвтоматическойРегистрацией = ФормаВопроса.ОбновлятьТолькоДляЭлементовСАвтоРегистрацией;
	
	Для Каждого СтрокаДереваМетаданных Из ДеревоОбмена.Строки Цикл
		
		Если СтрокаДереваМетаданных.Данные = "Константы" Тогда
			
			Для Каждого СтрокаНазваний Из СтрокаДереваМетаданных.Строки Цикл
				
				Если ТолькоДляОбъектовСАвтоматическойРегистрацией
					И СтрокаНазваний.РежимАвтоРегистрации = Ложь Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, Константы[СтрокаНазваний.Имя]);	
								
			КонецЦикла;
			
		ИначеЕсли СтрокаДереваМетаданных.Данные = "Справочники"
			ИЛИ СтрокаДереваМетаданных.Данные = "Документы"
			ИЛИ СтрокаДереваМетаданных.Данные = "ПланыВидовХарактеристик"
			ИЛИ СтрокаДереваМетаданных.Данные = "ПланыСчетов"
			ИЛИ СтрокаДереваМетаданных.Данные = "ПланыВидовРасчета"
			ИЛИ СтрокаДереваМетаданных.Данные = "БизнесПроцессы"
			ИЛИ СтрокаДереваМетаданных.Данные = "Задачи" Тогда
			
			Для Каждого СтрокаНазваний Из СтрокаДереваМетаданных.Строки Цикл
				
				Если ТолькоДляОбъектовСАвтоматическойРегистрацией
					И СтрокаНазваний.РежимАвтоРегистрации = Ложь Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				Если СтрокаДереваМетаданных.Данные = "Справочники" Тогда
					ПрефиксОбъектаМетаданных = "Справочник";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "Документы" Тогда	
					ПрефиксОбъектаМетаданных = "Документ";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "ПланыВидовХарактеристик" Тогда	
					ПрефиксОбъектаМетаданных = "ПланВидовХарактеристик";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "ПланыСчетов" Тогда	
					ПрефиксОбъектаМетаданных = "ПланСчетов";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "ПланыВидовРасчета" Тогда	
					ПрефиксОбъектаМетаданных = "ПланВидовРасчета";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "БизнесПроцессы" Тогда	
					ПрефиксОбъектаМетаданных = "БизнесПроцесс";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "Задачи" Тогда	
					ПрефиксОбъектаМетаданных = "Задача";
				КонецЕсли;
				
				ВыполнитьРегистрациюПервогоОбъектаДляУзла(ПрефиксОбъектаМетаданных, СтрокаНазваний.Имя);
																
			КонецЦикла;
			
		ИначеЕсли СтрокаДереваМетаданных.Данные = "Последовательности"
			ИЛИ СтрокаДереваМетаданных.Данные = "РегистрыСведений"
			ИЛИ СтрокаДереваМетаданных.Данные = "РегистрыНакопления"
			ИЛИ СтрокаДереваМетаданных.Данные = "РегистрыБухгалтерии"
			ИЛИ СтрокаДереваМетаданных.Данные = "РегистрыРасчета" Тогда
			
			Для Каждого СтрокаНазваний Из СтрокаДереваМетаданных.Строки Цикл
				
				Если ТолькоДляОбъектовСАвтоматическойРегистрацией
					И СтрокаНазваний.РежимАвтоРегистрации = Ложь Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				Если СтрокаДереваМетаданных.Данные = "Последовательности" Тогда
					ПрефиксОбъектаМетаданных = "Последовательность";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "РегистрыСведений" Тогда	
					ПрефиксОбъектаМетаданных = "РегистрСведений";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "РегистрыНакопления" Тогда	
					ПрефиксОбъектаМетаданных = "РегистрНакопления";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "РегистрыБухгалтерии" Тогда	
					ПрефиксОбъектаМетаданных = "РегистрБухгалтерии";
				ИначеЕсли СтрокаДереваМетаданных.Данные = "РегистрыРасчета" Тогда	
					ПрефиксОбъектаМетаданных = "РегистрРасчета";
				КонецЕсли;
				
				
				ВыполнитьРегистрациюРегистра(СтрокаДереваМетаданных.Данные, ПрефиксОбъектаМетаданных, СтрокаНазваний.Имя);
																
			КонецЦикла;
		
		КонецЕсли;	
					
	КонецЦикла;
	
	ПостроитьДеревоИзмененнныхОбъектовДляУзла();
		
КонецПроцедуры

Процедура КнопкаИзменитьНомераНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
		Предупреждение("Сначала необходимо выбрать узел");
		Возврат;
	КонецЕсли; 
	
	ФормаИзмененияНомеров = ПолучитьФорму("ИзменениеНомеровСообщений", ЭтаФорма);
	ФормаИзмененияНомеров.УзелОбмена = УзелОбмена;
	ФормаИзмененияНомеров.ОткрытьМодально();
	
КонецПроцедуры

Процедура ДеревоОбменаПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ДеревоОбменаПриАктивизацииСтроки(Элемент = Неопределено)
	
	КП_ТаблицаИзмененийОбновить();
	
КонецПроцедуры

//Процедура НаборЗаписейВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
//	
//	ЗначениеКолонки = ВыбраннаяСтрока[Колонка.Имя];
//	глПроверитьОткрытьСсылку(ЗначениеКолонки);

//КонецПроцедуры

//Процедура НаборЗаписейПередНачаломИзменения(Элемент, Отказ)
//	
//	Отказ = Истина;
//	
//КонецПроцедуры

//Процедура КлючОбъектаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
//	
//	ЗначениеКолонки = ВыбраннаяСтрока[Колонка.Имя];
//	глПроверитьОткрытьСсылку(ЗначениеКолонки);

//КонецПроцедуры

Процедура ОтображатьКоличествоОбъектовДляКоторыхЗарегисирированыИзмененияПриИзменении(Элемент)
	
	Если Не ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения Тогда
		ЭтаФорма.ОтображатьТолькоМетаданныеСИзменениями = Ложь;
	КонецЕсли; 
	ЭлементыФормы.ОтображатьТолькоМетаданныеСИзменениями.Доступность = ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения;
	ПостроитьДеревоИзмененнныхОбъектовДляУзла();
	
КонецПроцедуры

Процедура ОтображатьТолькоМетаданныеСИзменениямиПриИзменении(Элемент)
	
	ПостроитьДеревоИзмененнныхОбъектовДляУзла();

КонецПроцедуры

Процедура ТаблицаИзмененийВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Ложь
		Или Колонка.Имя = "НомерСообщения" 
		Или (Истина
			И Колонка.Имя = "Ссылка"
			И мТекущаяГруппаТипаМетаданных = "Ссылочный")
	Тогда
		КП_ТаблицаИзмененийОткрыть();
		СтандартнаяОбработка = Ложь;
		Возврат;
	Иначе
		КП_ТаблицаИзмененийОткрыть();
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли; 
	ирНеглобальный.ЯчейкаТабличногоПоляРасширенногоЗначения_ВыборЛкс(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийМенеджерТабличногоПоля(Кнопка)
	
	 Обработки.ирМенеджерТабличногоПоля.ПолучитьФорму(, ЭтаФорма, ).УстановитьСвязь(ЭлементыФормы.ТаблицаИзменений);
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийОткрыть(Кнопка = Неопределено)
	
	ВыбраннаяСтрока = ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока;
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПолноеИмяМД = ЭлементыФормы.ДеревоОбмена.ТекущаяСтрока.Данные;
	Если Найти(ПолноеИмяМД, "Константа") = 1 Тогда
		//Предупреждение(Данные.Значение, , Строка(Данные));
	Иначе
		Попытка
			Пустышка = Тип(СтрЗаменить(ПолноеИмяМД, ".", "Объект."));
			ЭтоСсылочныйОбъект = Истина;
		Исключение
			ЭтоСсылочныйОбъект = Ложь;
		КонецПопытки; 
		Если ЭтоСсылочныйОбъект Тогда
			//Данные.ПолучитьФорму().Открыть();
			ОткрытьЗначение(ВыбраннаяСтрока.Ссылка); // Ссылку надо брать, чтобы в управляемом режиме открывалась управляемая форма
		Иначе
			Отбор = Новый Структура;
			Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
				Если Колонка.Имя <> "НомерСообщения" Тогда
					Отбор.Вставить(Колонка.Имя, ВыбраннаяСтрока[Колонка.Имя]);
				КонецЕсли; 
			КонецЦикла;
			//ПараметрыФормы = Новый Структура("Отбор", Отбор);
			//ФормаСписка = ПолучитьФорму(ПолноеИмяМД + ".ФормаСписка", ПараметрыФормы);
			//Если ТипЗнч(ФормаСписка) = Тип("Форма") Тогда
			//	Для Каждого КлючИЗначение Из Отбор Цикл
			//		ЭлементОтбора = ФормаСписка.Отбор[КлючИЗначение.Ключ];
			//		ЭлементОтбора.Использование = Истина;
			//		ЭлементОтбора.Значение = КлючИЗначение.Значение;
			//	КонецЦикла; 
			//КонецЕсли; 
			//ФормаСписка.Открыть();
			ирНеглобальный.ОткрытьФормуСпискаСОтбором(ПолноеИмяМД, Отбор);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийРедакторОбъектаБДЯчейки(Кнопка)
	
	ирНеглобальный.ОткрытьСсылкуЯчейкиВРедактореОбъектаБДЛкс(ЭлементыФормы.ТаблицаИзменений);
	
КонецПроцедуры

Процедура КоманднаяПанельДереваОбъектовОткрытьФормуСписка(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.ДеревоОбмена.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Если ТекущаяСтрока.Уровень() = 1 Тогда
		Если ТекущаяСтрока.Родитель.Имя <> "Константа" Тогда
			ОткрытьФорму(ТекущаяСтрока.Данные + ".ФормаСписка");
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗагружатьИзмененияПриИзменении(Элемент)
	
	ПостроитьДеревоИзмененнныхОбъектовДляУзла();

КонецПроцедуры

Функция ПолучитьПостроительДляТаблицыИзменений(ВыбраннаяСтрока = Неопределено, МаксимальнаяПорция = 0)

	Если ВыбраннаяСтрока = Неопределено Тогда
		ВыбраннаяСтрока = ЭлементыФормы.ДеревоОбмена.ТекущаяСтрока;
	КонецЕсли; 
	ТекстВыбор = "_Т.*";
	Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
		ТекстВыбор = ТекстВыбор + ", ВЫБОР КОГДА ЕСТЬNULL(_Т.Ссылка.Ссылка, 0) = 0 ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ КАК ОбъектУдален, _Т.Ссылка.ПометкаУдаления КАК ПометкаУдаления";
	КонецЕсли; 
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ВыбраннаяСтрока.Данные);
	ТекстПервые = "";
	Если МаксимальнаяПорция > 0 Тогда
		ТекстПервые = " ПЕРВЫЕ " + Формат(МаксимальнаяПорция, "ЧГ=");
	КонецЕсли; 
	Построитель = Новый ПостроительЗапроса("ВЫБРАТЬ " + ТекстПервые + ТекстВыбор + " ИЗ " + ВыбраннаяСтрока.Данные + ".Изменения КАК _Т АВТОУПОРЯДОЧИВАНИЕ");
	Построитель.ЗаполнитьНастройки();
	Для Каждого ДоступноеПоле Из Построитель.ДоступныеПоля Цикл
		Если ДоступноеПоле.Имя = "НомерСообщения" Тогда
			Продолжить;
		КонецЕсли; 
		Построитель.Порядок.Добавить(ДоступноеПоле.Имя, ДоступноеПоле.Имя);
	КонецЦикла;
	Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
		ДоступноеПолеДата = Построитель.ДоступныеПоля.Ссылка.Поля.Найти("Дата");
		Если ДоступноеПолеДата <> Неопределено Тогда
			Построитель.ВыбранныеПоля.Добавить(ДоступноеПолеДата.ПутьКДанным, "Дата");
		КонецЕсли;
	КонецЕсли; 
	Построитель.ВыбранныеПоля.Удалить(Построитель.ВыбранныеПоля.Найти("Узел"));
	Построитель.Отбор.Добавить("Узел").Установить(УзелОбмена);
	
	// Антибаг 8.2.14 http://partners.v8.1c.ru/forum/thread.jsp?id=1017264#1017264
	Если ирКэш.Получить().ВерсияПлатформы >= 802014 Тогда
		Если мТекущаяГруппаТипаМетаданных = "Регистр" Тогда
			Для Каждого ОбщийРеквизит Из Метаданные.ОбщиеРеквизиты Цикл
				ВыбранноеПоле = Построитель.ВыбранныеПоля.Найти(ОбщийРеквизит.Имя);
				Если Истина
					И ВыбранноеПоле <> Неопределено
					И ОбъектМетаданных.Измерения.Найти(ОбщийРеквизит.Имя) = Неопределено
				Тогда
					Если ирНеглобальный.ЛиОбщийРеквизитИспользуетсяВОбъектеМетаданныхЛкс(ОбщийРеквизит, ОбъектМетаданных) Тогда
						Построитель.ВыбранныеПоля.Удалить(ВыбранноеПоле);
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли;
	Возврат Построитель;

КонецФункции

Процедура КП_ТаблицаИзмененийОбновить(Кнопка = Неопределено)
	
	СтруктураТекущейСтроки = Неопределено;
	ТекущаяКолонка = ЭлементыФормы.ТаблицаИзменений.ТекущаяКолонка;
	Если ТекущаяКолонка <> Неопределено Тогда
		ТекущаяКолонка = ТекущаяКолонка.Данные;
	КонецЕсли; 
	ТекущаяСтрокаИзменений = ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока;
	Если ТекущаяСтрокаИзменений <> Неопределено Тогда
		СтруктураТекущейСтроки = Новый Структура();
		Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
			СтруктураТекущейСтроки.Вставить("Ссылка", ТекущаяСтрокаИзменений.Ссылка);
		Иначе
			Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
				Если Колонка.Имя = "НомерСообщения" Тогда
					Продолжить;
				КонецЕсли; 
				СтруктураТекущейСтроки.Вставить(Колонка.Имя, ТекущаяСтрокаИзменений[Колонка.Имя]);
			КонецЦикла; 
		КонецЕсли; 
	КонецЕсли; 
	ТаблицаИзменений.Очистить();
	ТаблицаИзменений.Колонки.Очистить();
	ВыбраннаяСтрока = ЭлементыФормы.ДеревоОбмена.ТекущаяСтрока;
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	УровеньВыбраннойСтроки = ВыбраннаяСтрока.Уровень();
	ЭлементыФормы.ТаблицаИзменений.ТолькоПросмотр = (УровеньВыбраннойСтроки = 0);
	Если УровеньВыбраннойСтроки = 1 Тогда
		ОбновитьКоличествоЗарегистрированныхДляСтрокиДерева(ВыбраннаяСтрока);
		Попытка
			мМакетныйОбъект = Новый (СтрЗаменить(ВыбраннаяСтрока.Данные, ".", "Объект."));
			мТекущаяГруппаТипаМетаданных = "Ссылочный";
		Исключение
			Попытка
				мМакетныйОбъект = Новый (СтрЗаменить(ВыбраннаяСтрока.Данные, ".", "НаборЗаписей."));
				мТекущаяГруппаТипаМетаданных = "Регистр";
			Исключение
				мМакетныйОбъект = Новый (СтрЗаменить(ВыбраннаяСтрока.Данные, ".", "МенеджерЗначения."));
				мТекущаяГруппаТипаМетаданных = "Константа";
			КонецПопытки; 
		КонецПопытки;
		
		Построитель = ПолучитьПостроительДляТаблицыИзменений();
		ЧислоСтрокДляЗагрузки = ирНеглобальный.КонтрольРазмераВыборкиПользователемЛкс(Построитель);
		Если ЧислоСтрокДляЗагрузки > 0 Тогда
			Построитель = ПолучитьПостроительДляТаблицыИзменений(, ЧислоСтрокДляЗагрузки);
		КонецЕсли;
		// http://partners.v8.1c.ru/forum/thread.jsp?id=1034151#1034151
		//МаксимальныйРазмер = 500000;
		//Построитель = ПолучитьПостроительДляТаблицыИзменений(, МаксимальныйРазмер);
		
		мРезультатЗапроса = Построитель.Результат;
		ирНеглобальный.ЗагрузитьДинамическуюВыборкуВТабличноеПолеПервуюПорциюЛкс(ЭтаФорма, мРезультатЗапроса, мВыборкаРезультата,
			ЭлементыФормы.КП_ТаблицаИзменений);
		// Дорогая операция!
		ТаблицаИзменений = ирНеглобальный.ПолучитьТаблицуСКолонкамиБезТипаNullЛкс(ТаблицаИзменений,,, "НомерСообщения");
		Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
			ТаблицаИзменений.Колонки.ОбъектУдален.Заголовок = "Объект удален";
			ТаблицаИзменений.Колонки.ПометкаУдаления.Заголовок = "Ссылка.Пометка удаления";
			Если ТаблицаИзменений.Колонки.Найти("Дата") <> Неопределено Тогда
				ТаблицаИзменений.Колонки.Дата.Заголовок = "Ссылка.Дата";
			КонецЕсли; 
		КонецЕсли; 
		ТаблицаИзменений.Колонки.НомерСообщения.Заголовок = "Номер сообщения";
	Иначе
		мРезультатЗапроса = Неопределено;
		мВыборкаРезультата = Неопределено;
		ОбновитьРазмерДинамическойТаблицы();
	КонецЕсли;
	ЭлементыФормы.ТаблицаИзменений.СоздатьКолонки();
	Для Каждого КолонкаТП Из ЭлементыФормы.ТаблицаИзменений.Колонки Цикл
		Если Ложь
			Или КолонкаТП.Имя = "НомерСообщения" 
			Или (Истина
				И мТекущаяГруппаТипаМетаданных = "Ссылочный"
				И КолонкаТП.Имя <> "Ссылка")
		Тогда
			КолонкаТП.ТолькоПросмотр = Истина;
		Иначе
			КолонкаТП.ЭлементУправления.УстановитьДействие("ОкончаниеВводаТекста", Новый Действие("ПолеВводаКолонкиСсылка_ОкончаниеВводаТекста"));
		КонецЕсли;
		КолонкаТП.ТекстШапки = ТаблицаИзменений.Колонки[КолонкаТП.Имя].Заголовок;
	КонецЦикла;
	Если СтруктураТекущейСтроки <> Неопределено Тогда
		НоваяСтруктураТекущейСтроки = Новый Структура();
		Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
			Если Колонка.Имя = "НомерСообщения" Тогда
				Продолжить;
			КонецЕсли; 
			Если СтруктураТекущейСтроки.Свойство(Колонка.Имя) Тогда
				НоваяСтруктураТекущейСтроки.Вставить(Колонка.Имя, СтруктураТекущейСтроки[Колонка.Имя]);
			КонецЕсли; 
		КонецЦикла;
		НайденныеСтроки = ТаблицаИзменений.НайтиСтроки(НоваяСтруктураТекущейСтроки);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЭлементыФормы.ТаблицаИзменений.ТекущаяСтрока = НайденныеСтроки[0];
		КонецЕсли; 
	КонецЕсли; 
	Если ТекущаяКолонка <> Неопределено Тогда
		НоваяТекущаяКолонка = ЭлементыФормы.ТаблицаИзменений.Колонки.Найти(ТекущаяКолонка);
		Если НоваяТекущаяКолонка <> Неопределено Тогда
			ЭлементыФормы.ТаблицаИзменений.ТекущаяКолонка = НоваяТекущаяКолонка;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ГлавнаяКоманднаяПанельНовоеОкно(Кнопка)
	
	ЛксОткрытьНовоеОкноОбработки(ЭтотОбъект);
	
КонецПроцедуры

Процедура ДействияФормыОПодсистеме(Кнопка)
	
	ЛксОткрытьСправкуПоПодсистеме(ЭтотОбъект);
	
КонецПроцедуры

Процедура ТаблицаИзмененийПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ВыделенныеСтроки = ЭлементыФормы.ТаблицаИзменений.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Ответ = Вопрос("Вы действительно хотите удалить регистрацию по " + ВыделенныеСтроки.Количество() + " объектам?", РежимДиалогаВопрос.ОКОтмена);
	Если Ответ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	СтрокаДереваТекущихДанных = ЭлементыФормы.ДеревоОбмена.ТекущиеДанные;
	СтрокаОбновления = ЭлементыФормы.ДеревоОбмена.ТекущиеДанные;
	Индикатор = ЛксПолучитьИндикаторПроцесса(ВыделенныеСтроки.Количество(), "Удаление регистрации");
	НачальноеКоличество = ВыделенныеСтроки.Количество(); 
	Для СчетчикВыделенныеСтроки = 1 По НачальноеКоличество Цикл
		ВыделеннаяСтрока = ВыделенныеСтроки[НачальноеКоличество - СчетчикВыделенныеСтроки];
		ЛксОбработатьИндикатор(Индикатор);
		КлючУдаления = Неопределено;
		Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
			КлючУдаления = ВыделеннаяСтрока.Ссылка;
		ИначеЕсли мТекущаяГруппаТипаМетаданных = "Регистр" Тогда
			КлючУдаления = мМакетныйОбъект;
			// http://partners.v8.1c.ru/forum/thread.jsp?id=1034617#1034617
			//мМакетныйОбъект.Отбор.Сбросить();
			Для Каждого Колонка Из ТаблицаИзменений.Колонки Цикл
				Если Колонка.Имя = "НомерСообщения" Тогда
					Продолжить;
				КонецЕсли; 
				ЭлементОтбора = мМакетныйОбъект.Отбор[Колонка.Имя];
				ЭлементОтбора.Значение = ВыделеннаяСтрока[Колонка.Имя];
				ЭлементОтбора.Использование = Истина;
			КонецЦикла;
		ИначеЕсли мТекущаяГруппаТипаМетаданных = "Константа" Тогда
			КлючУдаления = мМакетныйОбъект;
		КонецЕсли; 
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, КлючУдаления);
		ТаблицаИзменений.Удалить(ВыделеннаяСтрока);
	КонецЦикла;
	ЛксОсвободитьИндикаторПроцесса();
	ОбновитьКоличествоЗарегистрированныхДляСтрокиДерева();
	//КП_ТаблицаИзмененийОбновить();

КонецПроцедуры

Процедура ТаблицаИзмененийПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура ТаблицаИзмененийПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	Объект = ПолучитьОбъектДанныхПоСтрокеИзменений();
	ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, Объект);
	ОбновитьКоличествоЗарегистрированныхДляСтрокиДерева();
	КП_ТаблицаИзмененийОбновить();
	
КонецПроцедуры

Процедура ТаблицаИзмененийПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ЭлементыФормы.КП_ТаблицаИзменений.Кнопки.Пустые.Пометка Тогда
		ирНеглобальный.ОтобразитьПустыеЗначенияВЯчейкахТабличногоПоля(ОформлениеСтроки);
	КонецЕсли;
	
КонецПроцедуры

Процедура КП_ТаблицаИзмененийПустые(Кнопка)
	
	Кнопка.Пометка = Не Кнопка.Пометка;
	ЭлементыФормы.ТаблицаИзменений.ОбновитьСтроки();
	
КонецПроцедуры

Процедура ТаблицаИзмененийПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если Не ОтменаРедактирования Тогда
		Если мТекущаяГруппаТипаМетаданных = "Ссылочный" Тогда
			Если Не ЗначениеЗаполнено(Элемент.ТекущиеДанные.Ссылка) Тогда
				Отказ = Истина;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПолеВводаКолонкиСсылка_ОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	ирНеглобальный.ПолеВвода_ОкончаниеВводаТекстаЛкс(Элемент, Текст, Значение, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПослеВосстановленияЗначений()
	
	УзелОбменаПриИзменении();

КонецПроцедуры

Процедура КП_ТаблицаИзмененийКонсольКомпоновки(Кнопка)
	
	Если ЭлементыФормы.ТаблицаИзменений.Колонки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	Построитель = ПолучитьПостроительДляТаблицыИзменений();
	// Из-за бага платформы пока так не стоит делать
	//Построитель.Текст = СтрЗаменить(Построитель.Текст, "АВТОУПОРЯДОЧИВАНИЕ", "");
	//Отчеты.ирКонсольКомпоновокДанных.Создать().ОткрытьПоЗапросу(, Новый Структура("Узел", УзелОбмена));
	Запрос = Построитель.ПолучитьЗапрос();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "АВТОУПОРЯДОЧИВАНИЕ", "");
	Отчеты.ирКонсольКомпоновокДанных.Создать().ОткрытьПоЗапросу(Запрос);
	
	//Отчеты.ирКонсольКомпоновокДанных.Создать().ОткрытьПоОбъектуМетаданных(ЭлементыФормы.ДеревоОбмена.ТекущиеДанные.Данные + ".Изменения", Новый Структура("Узел", УзелОбмена));
	
КонецПроцедуры

Процедура УзелОбменаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ирНеглобальный.ПолеВводаСИсториейВыбора_НачалоВыбораИзСпискаЛкс(Элемент, Метаданные().Имя);

КонецПроцедуры

Процедура КП_ТаблицаИзмененийЗагрузитьПолностью(Кнопка)
	
	ирНеглобальный.ЗагрузитьДинамическуюВыборкуВТабличноеПолеПолностьюЛкс(ЭтаФорма, мВыборкаРезультата, ЭлементыФормы.КП_ТаблицаИзменений);
	
КонецПроцедуры

Процедура ОбновитьРазмерДинамическойТаблицы() Экспорт

	ирНеглобальный.ПослеЗагрузкиДинамическойВыборкиВТабличноеПолеЛкс(ЭтаФорма, мВыборкаРезультата,
		ЭлементыФормы.КП_ТаблицаИзменений, ЭлементыФормы.КоличествоСтрокИзменений);

КонецПроцедуры // ОбновитьРазмерТаблицы()

ДеревоОбмена.Колонки.Добавить("Данные");

мСоответствиеСтрокДереваИМетаданных = Новый Массив();

ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения = Истина;
ЭлементыФормы.ОтображатьТолькоМетаданныеСИзменениями.Доступность = ОтображатьКоличествоОбъектовДляКоторыхЗарегистрированыИзменения;
ОтображатьТолькоМетаданныеСИзменениями = Истина;
